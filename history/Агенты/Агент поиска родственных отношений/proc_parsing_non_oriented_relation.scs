scp_program -> proc_search_of_parsing(*
  -> rrel_params: ...;;
  -> rrel_operators: ...(* 
    -> rrel_init: ..operator_parsing_print_1(*
      <- printNl;;
			-> rrel_1: rrel_fixed: rrel_scp_const: [Процедура: поиск неориентированных отношений];;
      => nrel_goto: ..operator_parsing_1;;
    *);;

		-> ..operator_parsing_1
			(*
			<- genEl;;
		  	-> rrel_1: rrel_assign: rrel_scp_var: rrel_node: rrel_const: _set_person;;

		  	=> nrel_goto: ..operator_parsing_2;;
			*);;

		/// Формируем множество персон
		-> ..operator_parsing_2
			(*
			<- searchSetStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: concept_male;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  -> rrel_3: rrel_assign: rrel_scp_var: _person;;

		  -> rrel_set_3: rrel_fixed: rrel_scp_var: _set_person;;

		  => nrel_goto: ..operator_parsing_3;;
			*);;

		-> ..operator_parsing_3
			(*
			<- searchSetStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: concept_female;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  -> rrel_3: rrel_assign: rrel_scp_var: _person;;

		  -> rrel_set_3: rrel_fixed: rrel_scp_var: _set_person;;

		  => nrel_then: ..operator_parsing_4;;
      => nrel_else: ..operator_parsing_14;;
			*);;
	
		/// текущая персона
		-> ..operator_parsing_4
			(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _set_person;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  -> rrel_3: rrel_assign: rrel_scp_var: _person;;

		  => nrel_then: ..operator_parsing_5;;
      => nrel_else: ..operator_parsing_14;;
			*);;
		-> ..operator_parsing_5(*
		 <- eraseEl;;
		 -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc;;

		 => nrel_goto: ..operator_parsing_6;;
		 *);;

		-> ..operator_parsing_6
			(*
			<- genEl;;
		  	-> rrel_1: rrel_assign: rrel_scp_var: rrel_node: rrel_const: _set_person_2;;

		  	=> nrel_goto: ..operator_parsing_7;;
			*);;
		/// формируем множество персон
		-> ..operator_parsing_7
			(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _person;;
		  -> rrel_2: rrel_assign: rrel_edge: rrel_const: rrel_scp_var: _arc_edge;;
		  -> rrel_3: rrel_assign: rrel_scp_var: _person_2;;
		  -> rrel_4: rrel_assign: rrel_scp_var: _arc;;
		  -> rrel_5: rrel_fixed: rrel_scp_const: nrel_spouse;;

		  -> rrel_set_3: rrel_fixed: rrel_scp_var: _set_person_2;;

		  => nrel_then: ..operator_parsing_8;;
      => nrel_else: ..operator_parsing_13;;
			*);;

			/// находим текущее отношение
		-> ..operator_parsing_8
			(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _person;;
		  -> rrel_2: rrel_assign: rrel_edge: rrel_const: rrel_scp_var: _arc_edge;;
		  -> rrel_3: rrel_assign: rrel_scp_var: _person_2;;

		 	=> nrel_then: ..operator_parsing_9;;
      => nrel_else: ..operator_parsing_13;;
			*);;
		-> ..operator_parsing_9(*
		 <- eraseEl;;
		 -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc_edge;;

		 => nrel_goto: ..operator_parsing_10;;
		 *);;

		/// генерируем связку
		-> ..operator_parsing_10
			(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: nrel_spouse;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
		  -> rrel_3: rrel_assign: rrel_scp_var: rrel_node: rrel_const: _ligalement;;

		  => nrel_goto: ..operator_parsing_11;;
			*);;
	
		-> ..operator_parsing_11
			(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _ligalement;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc_gen;;
		  -> rrel_3: rrel_fixed: rrel_scp_var: _person;;

		  => nrel_goto: ..operator_parsing_12;;
			*);;

		-> ..operator_parsing_12
			(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _ligalement;;
		  -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc_gen;;
		  -> rrel_3: rrel_fixed: rrel_scp_var: _person_2;;

		  => nrel_goto: ..operator_parsing_print_2;;
			*);;

		-> ..operator_parsing_print_2(*
      <- printEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _person_2;;
      => nrel_goto: ..operator_parsing_8;;
    *);;

		-> ..operator_parsing_13(*
		 <- eraseEl;;
		 -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set_person_2;;

		 => nrel_goto: ..operator_parsing_4;;
		 *);;

		-> ..operator_parsing_14(*
		 <- eraseEl;;
		 -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set_person;;

		 => nrel_goto: ..operator_parsing_return;;
		 *);;

    -> ..operator_parsing_return(*
      <- return;;
    *);;
  *);;
*);;
